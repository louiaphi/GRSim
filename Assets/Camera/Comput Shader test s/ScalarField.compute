#pragma kernel CSMain

RWTexture2D<float4> Result;

int width;
int height;

float scale;
float a;
float M;
float z;
float rim;
float rim2;

float EHRadius;

bool enableEventHorizon;
bool enableErgosphere;

float3 ColorSingularity;
float3 ColorPortal;
float3 ColorEventHorizon;
float3 ColorErgosphere;

bool BaaaaasicallySameSame(float x, float y, float e)
{
    if (abs(x - y) < e)
    {
        return true;
    }
    return false;
}

float CalcH(float r, float z, float M, float a)
{
    float r2 = r * r;
    float r4 = r2 * r2;
    return (M * r * r2) / (r4 + a * a * z * z);
}

float CalcR2(float4 pos, float a)
{
    float rho2 = pos.y * pos.y + pos.z * pos.z + pos.w * pos.w;
    float aa = a * a;
    float term = rho2 - aa;
    return 0.5 * (term + sqrt(term * term + 4.0 * aa * pos.w * pos.w));
}

// --- Kernel ---

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    
    if (id.x >= width || id.y >= height)
        return;

    float x = id.x - width * 0.5;
    float y = id.y - height * 0.5;

    float r = sqrt(CalcR2(float4(0, x, y, z), a));

    float value;

    if (abs(r) < 0.1) //singularity
    {
        value = -1; //value normally never negative so used for this
        if (x * x + y * y < a * a - rim && z == 0) //not singularity but portal
        {
            value = -2;
        }
    }
    else
    {
        r *= scale;
        value = CalcH(r, z, M, a);
    }
    if (enableEventHorizon && BaaaaasicallySameSame(x * x + y * y, (EHRadius * EHRadius) * (1 - ((z * z) / (EHRadius * EHRadius))), rim))
    {
        value = -3;
    }
    
    

    if (enableErgosphere)
    {
        float rho2 = r * r + a * a * (z * z) / (r * r + a * a); //moved in for maybe better efficiancy when disabled... if you really believe in it
        float gtt = 1.0 - (2.0 * M * r) / rho2; //ergosphere definition
        if (abs(gtt) < rim2)
            value = -4;
    }

    //return stuff
    float3 color;
    if (value == -1)
        color = ColorSingularity;
    else if (value == -2)
        color = ColorPortal;
    else if (value == -3)
        color = ColorEventHorizon;
    else if (value == -4)
        color = ColorErgosphere;
    else
        color = value.xxx; //fancy syntax i must say
    Result[id.xy] = float4(color, 1);

}
